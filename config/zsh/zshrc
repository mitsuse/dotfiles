# set the type to each environment variable.
typeset -U path PATH

# save command history
HISTFILE=$HOME/.zsh_history
HISTSIZE=10000
SAVEHIST=10000
setopt hist_ignore_dups
setopt hist_reduce_blanks
setopt share_history

zshaddhistory() {
    local line=${1%%$'\n'}
    local cmd=${line%% *}
    [[ "${cmd}" != "rm" && "${cmd}" != "mv" ]]
}

# search history with history-search-end
autoload -U history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^p" history-beginning-search-backward-end
bindkey "^n" history-beginning-search-forward-end

# use vi-like keybinds and set the default editor to vim
bindkey -v
export EDITOR=vim

# misc
alias sudo='sudo '
# alias tp='pyenv exec trash-put'
export PATH=$HOME/bin:$HOME/dotfiles/bin:$PATH
autoload -U compinit && compinit
autoload -U predict-on && predict-on
compdef -d ssh
setopt auto_cd

# load more specific config files
# for CONFIG_PATH in $HOME/.zshrc_*
# load .zshrc_other first
[ -f $HOME/.zshrc_command ] && source $HOME/.zshrc_command
[ -f $HOME/.zshrc_go ] && source $HOME/.zshrc_go
[ -f $HOME/.zshrc_python ] && source $HOME/.zshrc_python
[ -f $HOME/.zshrc_ruby ] && source $HOME/.zshrc_ruby
[ -f $HOME/.zshrc_node ] && source $HOME/.zshrc_node
[ -f $HOME/.zshrc_brew ] && source $HOME/.zshrc_brew
[ -f $HOME/.zshrc_tools ] && source $HOME/.zshrc_tools
[ -f $HOME/.zshrc_osx ] && source $HOME/.zshrc_osx
[ -f $HOME/.zshrc_arch ] && source $HOME/.zshrc_arch
[ -f $HOME/.zshrc_other ] && source $HOME/.zshrc_other

# use a simple prompt
autoload -U add-zsh-hook
autoload -U vcs_info
zstyle ':vcs_info:*' enable git hg
zstyle ':vcs_info:*' formats '(%s) [%b]'

function update_vcs_info_msg() {
    psvar=()
    vcs_info   
    [[ -n "$vcs_info_msg_0_" ]] && psvar[1]="$vcs_info_msg_0_"
}

add-zsh-hook precmd update_vcs_info_msg

autoload -U colors && colors
PROMPT="%B%{${fg[yellow]}%}%~ %1(v|%v|)%b
%{${reset_color}%}[${USER}%B@${HOST}%b]$ "
